{## Data Context Interaction
 # - Separate perspectives of a program

  https://en.wikipedia.org/wiki/Data,_context_and_interaction

 #} {;
  {## Data Context Interaction #}
  export edh'dci = bookmark()

  # %% #
  data Point( x, y, ) pass

  # %% #

  {##
   # Root class of role classes

All role classes should suport instance construction from a symbolic keyword argument `@(Role.ddo)`

   #}
  class Role {

    {## Symbolic name for Domain Data Object #}
    symbol @ddo

    method __init__( *, @ddo as this.@ddo, *** ) scope(that).eval(
      expr extends {$ this.@ddo $}
    )

    method __repr__() {
      repr(this.@ddo) ++ ' ~assuming~ ' ++ repr(constructor(that))
    }

  }

  # %% #

  class OriginPoint { extends Role

    method translate( p ) case p of { Point( x, y, ) } -> {
      case super.@(Role.ddo) of { Point( x= ox, y= oy, ) } -> {
        Point( x - ox, y - oy, )
      }
    }

  }

  # %% #

  {##
   # Let an object assuming a role class

A role class should construct instances with a symbolic keyword argument from the symbol `@(Role.ddo)`. Alternatively, it can override this operation by providing a rhs magic `method ( ~assuming~ . ) { ... }` to return the assumed role instance.

   #}
  operator 10 (~assuming~) (lhv, rhv) {
    default rhv( @(Role.ddo)= lhv )
  }


  # %% #
  pt = Point( 3, 5, )
  ; o = pt ~assuming~ OriginPoint

  # %%
  o.translate( Point( 2, 7, ) )

  # %%
  o.x

  # %%
  o.y

  # %#
}
